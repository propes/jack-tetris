class Main {
    function void main() {
        var Block block;
        var boolean continue;
        var int cycle, key, LEFT_KEY, RIGHT_KEY, DOWN_KEY;

        let LEFT_KEY = 130;
        let RIGHT_KEY = 132;
        let DOWN_KEY = 133;

        do Board.init();
        do Board.draw();

        let continue = true;
        while (continue) {
            let block = Block.new(-2, 4); // Block starts off board
            do block.draw();

            while (block.canMoveDown()) {
                do block.moveDownAndRedraw();

                let cycle = 0;
                while (cycle < 5) {
                    let key = Keyboard.keyPressed();
                    if (key = LEFT_KEY) {
                        do block.moveLeftAndRedraw();
                    }
                    if (key = RIGHT_KEY) {
                        do block.moveRightAndRedraw();
                    }
                    if (key = DOWN_KEY) {
                        do block.dropAndRedraw();
                    }

                    do Sys.wait(100);
                    let cycle = cycle + 1;
                }
            }

            if (~block.isOnBoard()) {
                let continue = false;
            } else {
                do block.fix();
            }

            do block.dispose();
        }

        return;
    }
}